module Embedded_RKs
    
  use ODE_Interface
   
   implicit none
   
   private
   public ::  set_eRK_tolerance, & ! set the error tolerance 
              set_eRK_name,      & ! set the RK name of the family
              ERK_scheme,        & ! RK temporal scheme 
              get_eRK_effort       ! get the number of the requiered time steps  
    
   ! *** Runge Kutta Butcher array and orders of the RKs  
         real, allocatable :: k(:,:)
         real, save, allocatable :: a(:,:), b(:), bs(:), c(:) 
         integer, save :: q(2)
   
   ! *** RK selection and Tolerance
         real, save:: RK_Tolerance
         character (len=20), save :: RK_Method
         logical, save :: Method_selection = .false.
         integer, save :: N_eRK_effort = 0 
      
    contains

subroutine set_eRK_name( name)
      character(len=*), intent(in) :: name 
      
      RK_Method = name
      Method_selection = .true.
      if (allocated(k)) deallocate(k)
      N_eRK_effort  = 0     
      
end subroutine

integer function get_eRK_effort() result(N) 
     
      N = N_eRK_effort
      
end function 

subroutine set_eRK_tolerance( eps)
      real, intent(in) :: eps
      
  N_eRK_effort  = 0     
  RK_Tolerance = eps
    
end subroutine
 
!**************************************************************************
! It calculates one time step of a system of ODES by means of 
! Embedded Runge Kutta with variable step selection. 
! To detemine the time step, the estimated error is calculated based on two RKs.
! One with order q and one with order q-1.
! The method and the tolerance can be previously selected
!
! Inputs: 
!         F      : system of equations 
!         t1, t2 : time step to integrate 
!         U1     : initial conditions 
!
! Outputs: 
!         U1     : solution at time t2 with error less than tolerance 
!
! Authors: 
!         Francisco Javier Escoto Lopez (2018)
!         Juan A. Hernandez Ramos       (2019)
!***************************************************************************  
subroutine ERK_scheme( F, t1, t2, U1, U2, ierr )   
       procedure (ODES) :: F 
       real, intent(in) :: t1, t2, U1(:)
       real, intent(out) ::  U2(:)
       integer, intent(out) :: ierr 
       
       real :: V1(size(U1)) , V2(size(U1)), h, t
       integer ::  i, N
       
 ! *** Check if a method has been selected
       if (.not.Method_selection) then
           RK_Method = "DOPRI54"
           RK_Tolerance = 1d-4
       end if
       
 ! *** First order q solution and Second order q+1
 !     for initial step size
       call RK_scheme( RK_Method,  "First", F, t1, t2, U1, V1 )    
       call RK_scheme( RK_Method, "Second", F, t1, t2, U1, V2 )
       
 ! *** Local error estimation and step size calculation
 !     to satisfy tolerance condition
       h = t2 - t1
       h =  min( h, Step_size(V1 - V2, RK_Tolerance, minval(q), h) )
       
 ! *** Grid for the new step
       N = int( (t2 - t1) / h  ) +1
       h = (t2 - t1) / N
           
 ! *** Solution for embedded grid
       V1 = U1  ; V2 = U1 
       do i = 0, N - 1
          t = t1 + i * (t2 - t1) / N
          V1 = V2
          call RK_scheme( RK_Method, "First", F, t, t + h, V1, V2 )                    
       end do
      
       U2 = V2 
       
       ierr = 0
end subroutine

!*******************************************************************************
!  Runke-Kutta formulas for explicit schemes based on Butcher array 
!  First RK scheme creates Butcher's array and calculates k_i values
!  Second RK scheme uses k_i previously calculated
!*******************************************************************************  
subroutine RK_scheme( name, tag , F, t1, t2, U1, U2 )   
       character(len=*), intent(in) :: name , tag
       procedure (ODES) :: F
       real, intent(in) :: t1, t2, U1(:)
       real, intent(out) ::   U2(:)
       
       real :: Up( size(U1) ), h
       integer ::  i, j, Ne
        
       h = t2 - t1
       
 ! *** Solution for the first RK       
       if ( tag == "First" ) then
           
          call Butcher_array( name, Ne ) 
          if (.not.allocated(k)) allocate ( k( Ne, size(U1) ) ) 
          do i = 1, Ne
                      
            Up = U1 
            do j=1, i-1
               Up = Up + h * a(i,j) * k(j, :)
            end do  
                                     
            k(i,:) = F( Up, t1 + c(i) * h )

          end do
          N_eRK_effort = N_eRK_effort + Ne
          
          U2 = U1 + h * matmul( b, k ) 
          
 ! *** Solution for the second RK           
       elseif ( tag == "Second") then 
       
          U2 = U1 + h * matmul( bs, k ) 
          deallocate(k)
          
       end if
       
end subroutine

! ******************************************************************
! It calculates the new step size as a function of estimated error 
!
!  dU(Error) = k h**(q+1) 
!  Tolerance = k Step_size **(q+1)  
!  INPUTS: 
!          dU(:)     : Estimated error 
!          Tolerance : max permitted error 
!          q         : order of the error 
! OUTPUTS: 
!           Step_size : new temporal step 
!******************************************************************
real function Step_size( dU, tolerance, q, h  )
     real, intent(in) :: dU(:), tolerance, h
     integer, intent(in) :: q
    
    real :: normT
    normT = norm2(dU) 
    
    if (normT > tolerance) then 
        
           Step_size = h * (tolerance/normT)**(1./(q+1))
    else 
           Step_size = h
    end if
    
end function 

! ******************************************************************
! Butcher array for different Runge-Kutta names
!******************************************************************
subroutine Butcher_array( name, Ne ) 

   character(len=*), intent(in) :: name 
   integer, intent(out) :: Ne
   
    if   (name=="HeunEuler21") then 
      q = [2,1]
      Ne = 2 
      if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a, b, bs, c)
      end if
      
      allocate (a(Ne, Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
      
      c(:) = [ 0., 1. ]
      !-------------------------------------------------------------------------------------
      a(1,:) = [  0. ]
      a(2,:) = [  1. ]
      !-------------------------------------------------------------------------------------
      b(:)  = [ 1./2, 1./2 ]
      bs(:) = [ 1.,    0.  ]
   
    else if   (name=="RK21") then 
      q = [2,1]
      Ne = 3 
      if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a, b, bs, c)
      end if
      
      allocate (a(Ne, Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
      
      c(:) = [ 0., 0.5, 1. ]
      !-------------------------------------------------------------------------------------
      a(1,:) = [  0., 0. ]
      a(2,:) = [  1./2, 0. ]
      a(3,:) = [  1./256,  255./256	]
      !-------------------------------------------------------------------------------------
      b(:)  = [ 1./256,	255./256,	0. ]
      bs(:) = [ 1./512,	255./256,	1./512 ]
      
   else if   (name=="BogackiShampine") then 
      q = [3,2]
      Ne = 4 
      if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a, b, bs, c)
      end if
      
      allocate (a(Ne, Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
      
      c(:) = [ 0., 1./2, 3./4, 1. ]
      !-------------------------------------------------------------------------------------
      a(1,:) = [  0., 0., 0.            ]
      a(2,:) = [ 1./2, 0., 0.           ]
      a(3,:) = [ 0.,	3./4, 0.    	]
      a(4,:) = [ 2./9,	1./3,	4./9 	]
      !-------------------------------------------------------------------------------------
      b(:)  = [ 2./9,	1./3,	4./9,	0. ]
      bs(:) = [ 7./24,	1./4,	1./3,	1./8 ]
 
      
   else if     (name=="DOPRI54") then 
      q = [5,4]
      Ne = 7 
      if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a,b,bs,c)
      end if
      
      allocate (a(Ne,Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
      
      c(:) = [ 0., 1./5, 3./10, 4./5, 8./9, 1., 1. ]
      !-------------------------------------------------------------------------------------
      a(1,:) = [          0.,           0.,           0.,         0.,           0.,     0. ]
      a(2,:) = [      1./5  ,           0.,           0.,         0.,           0.,     0. ]
      a(3,:) = [      3./40 ,        9./40,           0.,         0.,           0.,     0. ]
      a(4,:) = [     44./45 ,      -56./15,        32./9,         0.,           0.,     0. ]
      a(5,:) = [ 19372./6561, -25360./2187,  64448./6561,  -212./729,           0.,     0. ]
      a(6,:) = [  9017./3168,    -355./33 ,  46732./5247,    49./176, -5103./18656,     0. ]
      a(7,:) = [    35./384 ,           0.,    500./1113,   125./192, -2187./6784 , 11./84 ]
      !-------------------------------------------------------------------------------------
      b(:)  = [ 35./384   , 0.,   500./1113,  125./192,  -2187./6784  ,  11./84  ,     0.]
      bs(:) = [5179./57600, 0., 7571./16695,  393./640, -92097./339200, 187./2100, 1./40 ]  
    

   !   bs(:) = [ 71./57600, 0., -71./16695, 71./1920, -17253./339200, 22./525, -1./40 ] 
   !   b(:) = [ -12715105075./11282082432, 0., 87487479700./32700410799, -10690763975./1880347072, 701980252875./199316789632, -1453857185./822651844, 69997945./29380423 ]   
      
      
   else if (name=="CashKarp") then 
       q = [5,4] 
       Ne = 6 
       if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a,b,bs,c)
       end if
       allocate (a(Ne,Ne-1), b(Ne), bs(Ne), c(Ne)  ) 

       c(:) = [ 0., 1./5, 3./10, 3./5, 1., 7./8 ]
      !-------------------------------------------------------------------------------------
       a(1,:) = [ 0.,          0.,       0.,         0.,            0. ] 
       a(2,:) = [ 1./5,        0.,       0.,         0.,            0. ] 
       a(3,:) = [ 3./40,       9./40,    0.,         0.,            0. ] 
       a(4,:) = [ 3./10,      -9./10,    6./5,       0.,            0. ] 
       a(5,:) = [ -11./54,     5./2,    -70./27,     35./27,        0. ] 
       a(6,:) = [ 1631./55296, 175./512, 575./13824, 44275./110592, 253./4096 ] 
      !-------------------------------------------------------------------------------------

       b(:)  = [    37./378, 0.,     250./621,     125./594,         0., 512./1771]
       bs(:) = [2825./27648, 0., 18575./48384, 13525./55296, 277./14336,     1./4 ]    
       
   else if (name=="Fehlberg54") then 
       q = [5,4]
       Ne = 6 
       if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a,b,bs,c)
       end if
       allocate (a(Ne,Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
       
      c(:) = [ 0., 1./4, 3./8, 12./13, 1., 1./2 ]
      !-------------------------------------------------------------------------------------
       a(1,:) = [ 0.        ,   0.       ,  0.        ,    0.     ,            0. ] 
       a(2,:) = [ 1./4      ,   0.       ,  0.        ,    0.     ,            0. ] 
       a(3,:) = [ 3./32     ,   9./32    ,  0.        ,    0.     ,            0. ] 
       a(4,:) = [ 1932./2197, -7200./2197, 7296./2197 ,    0.     ,            0. ] 
       a(5,:) = [ 439./216  ,  -8.       , 3680./513  , -845./4104,            0. ] 
       a(6,:) = [ -8./27    ,   2.       , -3544./2565, 1859./4104,       -11./40 ] 
      !-------------------------------------------------------------------------------------
       b(:)  = [ 16./135, 0., 6656./12825, 28561./56430, -9./50, 2./55]
       bs(:) = [25./216, 0., 1408./2565, 2197./4104, -1./5 , 0. ]    
       
   else if (name=="Fehlberg87") then 
       q = [8,7]
       Ne = 13 
       if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a,b,bs,c)
       end if
       allocate (a(Ne,Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
       
       c(:) = [ 0., 2./27, 1./9, 1./6, 5./12, 1./2, 5./6, 1./6, 2./3 , 1./3,   1., 0., 1.]
      !-------------------------------------------------------------------------------------
       a(1,:)  = [ 0., 0., 0., 0., 0., 0., 0., 0., 0., 0., 0., 0.] 
       a(2,:)  = [ 2./27, 0., 0., 0., 0., 0., 0.,  0., 0., 0., 0., 0.] 
       a(3,:)  = [ 1./36 , 1./12, 0., 0., 0., 0., 0.,  0.,0., 0., 0., 0.] 
       a(4,:)  = [ 1./24 , 0., 1./8 , 0., 0., 0., 0., 0., 0., 0., 0., 0.] 
       a(5,:)  = [ 5./12, 0., -25./16, 25./16., 0., 0., 0., 0., 0., 0., 0., 0.]
       a(6,:)  = [ 1./20, 0., 0., 1./4, 1./5, 0., 0.,0., 0., 0., 0., 0.] 
       a(7,:)  = [-25./108, 0., 0., 125./108, -65./27, 125./54, 0., 0., 0., 0., 0., 0.] 
       a(8,:)  = [ 31./300, 0., 0., 0., 61./225, -2./9, 13./900, 0., 0., 0., 0., 0.] 
       a(9,:)  = [ 2., 0., 0., -53./6, 704./45, -107./9, 67./90, 3., 0., 0., 0., 0.] 
       a(10,:) = [-91./108, 0., 0., 23./108, -976./135, 311./54, -19./60, 17./6, -1./12, 0., 0., 0.] 
       a(11,:) = [ 2383./4100, 0., 0., -341./164, 4496./1025, -301./82, 2133./4100, 45./82, 45./164, 18./41, 0., 0.] 
       a(12,:) = [ 3./205, 0., 0., 0., 0., -6./41, -3./205, -3./41, 3./41, 6./41, 0., 0.]
       a(13,:) = [ -1777./4100, 0., 0., -341./164, 4496./1025, -289./82, 2193./4100, 51./82, 33./164, 19./41, 0.,  1.]
      !-------------------------------------------------------------------------------------
       b(:)  = [ 41./840, 0., 0., 0., 0., 34./105, 9./35, 9./35, 9./280, 9./280, 41./840, 0., 0.] 
       bs(:) = [ 0., 0., 0., 0., 0., 34./105, 9./35, 9./35, 9./280, 9./280, 0., 41./840, 41./840]     
     
   else if (name=="Verner65") then 
       q = [6,5]
       Ne = 8 
       if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a,b,bs,c)
       end if
       allocate (a(Ne,Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
       
       c(:) = [ 0., 1./6, 4./15, 2./3, 5./6, 1., 1./15, 1. ]
      !-------------------------------------------------------------------------------------
       a(1,:) = [ 0. , 0.,  0., 0.,  0. ,  0. ,   0. ] 
       a(2,:) = [ 1./6 , 0.,  0., 0.,  0. ,  0. ,   0. ]  
       a(3,:) = [ 4./75 , 16./75,  0., 0.,  0. ,  0. ,   0. ]  
       a(4,:) = [ 5./6 , -8./3,  5./2, 0.,  0. ,  0. ,   0. ]  
       a(5,:) = [ -165./64 , 55./6, -425./64, 85./96,  0. ,  0. ,   0. ]  
       a(6,:) = [ 12./5, -8., 4015./612, -11./36,  88./255 ,  0. ,   0. ]  
       a(7,:) = [ -8263./15000, 124./75, -643./680, -81./250, 2484./10625 ,  0. ,   0. ]  
       a(8,:) = [ 3501./1720 , -300./43, 297275./52632, -319./2322, 24068./84065 ,  0. ,   3850./26703 ]  
      !-------------------------------------------------------------------------------------
       b(:)  = [ 3./40 , 0.,  875./2244, 23./72, 264./1955,    0., 125./11592, 43./616 ]
       bs(:) = [13./160, 0., 2375./5984,  5./16,    12./85, 3./44,         0.,      0. ] 
       
   else if (name=="RK65") then
       q = [6,5]
       Ne = 8 
       if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a,b,bs,c)
       end if
       allocate (a(Ne,Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
       
       c(:) = [ 0., 1./10, 2./9, 3./7, 3./5, 4./5, 1., 1. ]
      !-------------------------------------------------------------------------------------
       a(1,:) = [ 0.             ,  0.        ,  0.                , 0.               ,  0.              ,  0.             ,   0. ]
       a(2,:) = [ 1./10          ,  0.        ,  0.                , 0.               ,  0.              ,  0.             ,   0. ]
       a(3,:) = [ -2./81         ,  20./81    ,  0.                , 0.               ,  0.              ,  0.             ,   0. ]
       a(4,:) = [ 615./1372      ,  -270./343 ,  1053./1372        , 0.               ,  0.              ,  0.             ,   0. ]
       a(5,:) = [ 3243./5500     ,  -54./55   ,  50949./71500      , 4998./17875      ,  0.              ,  0.             ,   0. ]
       a(6,:) = [ -26492./37125  ,  72./55    ,  2808./23375       , -24206./37125    ,  338./459        ,  0.             ,   0. ]     
       a(7,:) = [ 5561./2376     , -35./11    ,  -24117./31603     , 899983./200772   ,  -5225./1836     ,  3925./4056     ,   0. ]  
       a(8,:) = [ 465467./266112 , -2945./1232,  -5610201./14158144, 10513573./3212352,  -424325./205632 ,  376225./454272 ,   0. ]  
      !-------------------------------------------------------------------------------------
       b(:)  = [ 61./864   , 0., 98415./321776, 16807./146016 , 1375./7344 , 1375./5408, -37./1120, 1./10]
       bs(:) = [ 821./10800, 0., 19683./71825 , 175273./912600,  395./3672 ,  785./2704,     3./50,   0. ] 
       
   else if (name=="RK87") then 
       q = [8,7]
       Ne = 13 
       if ( allocated(a).or.allocated(b).or.allocated(bs).or.allocated(c) ) then
          deallocate(a,b,bs,c)
       end if
       allocate (a(Ne,Ne-1), b(Ne), bs(Ne), c(Ne)  ) 
       
       c(:) = [ 0., 1./18, 1./12, 1./8, 5./16, 3./8, 59./400, 93./200, 5490023248./9719169821, 13./20, 1201146811./1299019798, 1., 1.]
      !-------------------------------------------------------------------------------------
       a(1,:) = [ 0.                     , 0.   ,   0.   , 0.                       , 0., 0., 0., 0., 0., 0., 0., 0.]                                                                                       
       a(2,:) = [ 1./18                  , 0.   ,   0.   , 0.                       , 0., 0., 0., 0., 0., 0., 0., 0.]                                                                                       
       a(3,:) = [ 1./48                  , 1./16,   0.   , 0.                       , 0., 0., 0., 0., 0., 0., 0., 0.]                                                                                       
       a(4,:) = [ 1./32                  , 0.   ,   3./32, 0.                       , 0., 0., 0., 0., 0., 0., 0., 0.]                                                                                       
       a(5,:) = [ 5./16                  , 0.   , -75./64, 75./64                   , 0., 0., 0., 0., 0., 0., 0., 0.]                                                                                       
       a(6,:) = [ 3./80                  , 0.   ,   0.   , 3./16                    , 3./20, 0., 0., 0., 0., 0.] 
       a(7,:) = [ 29443841./614563906    , 0.   ,   0.   , 77736538./692538347      , -28693883./1125000000, 23124283./1800000000, 0., 0., 0., 0., 0., 0.] 
       a(8,:) = [ 16016141./946692911    , 0.   ,   0.   , 61564180./158732637      , 22789713./633445777, 545815736./2771057229, -180193667./1043307555, 0., 0., 0., 0., 0.] 
       a(9,:) = [ 39632708./573591083    , 0.   ,   0.   , -433636366./683701615    , -421739975./2616292301, 100302831./723423059, 790204164./839813087, 800635310./3783071287, 0., 0., 0., 0.] 
       a(10,:) = [ 246121993./1340847787 , 0.   ,   0.   , -37695042795./15268766246, -309121744./1061227803, -12992083./490766935, 6005943493./2108947869, 393006217./1396673457, 123872331./1001029789, 0., 0., 0.] 
       a(11,:) = [ -1028468189./846180014, 0.   ,   0.   , 8478235783./508512852    , 1311729495./1432422823, -10304129995./1701304382, -48777925059./3047939560, 15336726248./1032824649, -45442868181./3398467696, 3065993473./597172653, 0., 0.]   
       a(12,:) = [ 185892177./718116043  , 0.   ,   0.   , -3185094517./667107341   , -477755414./1098053517, -703635378./230739211, 5731566787./1027545527, 5232866602./850066563, -4093664535./808688257, 3962137247./1805957418, 65686358./487910083, 0.]  
       a(13,:) = [ 403863854./491063109  , 0.   ,   0.   , -5068492393./434740067   , -411421997./543043805, 652783627./914296604, 11173962825./925320556, -13158990841./6184727034, 3936647629./1978049680, -160528059./685178525, 248638103./1413531060, 0.] 
      !-------------------------------------------------------------------------------------
       b(:)  = [ 14005451./335480064, 0., 0., 0., 0., -59238493./1068277825, 181606767./758867731, 561292985./797845732, -1041891430./1371343529, 760417239./1151165299, 118820643./751138087, -528747749./2220607170, 1./4] 
       bs(:) = [ 13451932./455176623, 0., 0., 0., 0., -808719846./976000145, 1757004468./5645159321, 656045339./265891186, -3867574721./1518517206, 465885868./322736535, 53011238./667516719, 2./45, 0.]     
     
   else
       
       write(*,*) " Error Butcher array: " 
       write(*,*) name // " is not available" 
       read(*,*)
       stop 
       
   end if 
   
end subroutine   
  

end module 